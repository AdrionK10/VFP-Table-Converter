local lcTextFile, lcExtension, lcFileType, lcHeading 
set safe on
set fullpath on
lcSourceFile = getfile("" , "Open", "Open Source", 0)

*lcTextFile = StrTran(lcSourceFile , "." ,"")+".txt"
lcExtension = Upper(JustExt(lcSourceFile))

lcTextFile = addbs(justpath(lcsourcefile))+juststem(lcsourcefile)+ lcExtension + ".txt"
*lcTextFile = JustStem(lcSourceFile) + lcExtension + ".txt"

lcFileType = "*"
lcHeading = "* Generated by SourceToText "+chr(13)+chr(10)

clear

do case
case lcExtension = "SCX"
	lcFileType = "K"
case lcExtension = "LBX"
	lcFileType = "B"
case lcExtension = "MNX"
	lcFileType = "M"
case lcExtension = "FRX"
	lcFileType = "R"
case lcExtension = "VCX"
	lcFileType = "V"
case lcExtension = "DCX"
	lcFileType = "d"
Otherwise 	
	lcFileType = "*"
endcase

* These are the project type identifiers for the files
*#define PRJTYPE_FORM		"K"
*#define PRJTYPE_LABEL		"B"
*#define PRJTYPE_MENU		"M"
*#define PRJTYPE_REPORT		"R"
*#define PRJTYPE_VCX			"V"
*#define PRJTYPE_DBC			"d"

If lcFileType = "*"
	=messagebox("Wrong file selected")
Else
	* ?strToFile(lcHeading  ,lcTextFile , .F.)
	do home()+"scctext" with lcSourceFile, lcFileType , lcTextFile , .T.
Endif

if file(lcTextFile)
	=MessageBox("Text file saved to "+lcTextFile)
	store lcTextFile to _cliptext
	IF MESSAGEBOX("Do you want to produce text file excluding comments?" ,36+256 , "Exclude Comments?") = 6
		Do GenerateTextFileWithoutComments with lcTextFile
	ENDIF
endif
*-------------------------------------------------------
* Procedure...: GenerateTextFileWithoutComments
* Called by...:
* Abstract....:
* Parameters..:
* Notes.......:
*-------------------------------------------------------
PROCEDURE GenerateTextFileWithoutComments(tcTextFileWithComments)
local lnArea, lcSetDelete, lcTextFileNoComments
lcSetDelete = SET("Deleted")
lnArea = SELECT(0)

WAIT "One Moment Please ...Generating Text file without comments." WINDOW NOWAIT

set deleted off

lcTextFileNoComments  = addbs(justpath(tcTextFileWithComments))+juststem(tcTextFileWithComments)+ ".noc"

IF FILE(lcTextFileNoComments)
	DELETE FILE (lcTextFileNoComments)
ENDIF

STRTOFILE( "* Generated by SourceToText "+chr(13)+chr(10) ,  (lcTextFileNoComments) , .F.)

create cursor cuText ;
(cText1 C (200) ,;
 cText2 C (200) ,;
 cText3 C (200) ,;
 cText4 C (200) ,;
 cText5 C (200) ,;
 lcomment l(1)  )
 
append from (tcTextFileWithComments) type sdf

Local lncuTextScanRec
lncuTextScanRec = 1
SELECT cuText
GO TOP

SCAN
 lncuTextScanRec = RECNO("cuText")
 WAIT "Generating Text file without comments."+CHR(13)+ " on "+TRANSFORM(RECNO()) + " of "+TRANSFORM(RECC()) WINDOW NOWAIT

 IF LEFT(LTRIM(cText1),1) = "*"
 * nothing, skip this one
 ELSE
 	scatter memvar
 	STRTOFILE( m.cText1 + m.cText2 + m.cText3 + m.cText4 + m.cText5 + CHR(13) ,  (lcTextFileNoComments) , .T.)
 ENDIF
 SELECT cuText
 GO lncuTextScanRec
ENDSCAN 

WAIT CLEAR

if file(lcTextFileNoComments)
	=MessageBox("Text file w/o comments saved to "+lcTextFileNoComments)
	store lcTextFileNoComments to _cliptext
Endif

IF USED("cuText")
 USE IN cuText
ENDIF

set deleted &lcSetDelete 
SELECT(lnArea)
RETURN
ENDPROC &&* GenerateTextFileWithoutComments